model{
  for(i in 1:n){
    Snet[i] <- (1 - albedo) * Si[i]
    THi[i] <- eSky * Sigma * (Tair[i] + 273.15)^4
    THo[i] <- eSur * Sigma * (Tsur[i] + 273.15)^4
    G[i] <- Cgrnd * (Snet[i] + THi[i] - THo[i])
    H[i] <- Cconv * WS[i]^Aconv * (Tsur[i] - Tair[i])
    
    LE[i] <- Snet[i] + THi[i] - THo[i] - G[i] - H[i]
    Lambda[i] <- 2502 - 2.308*Tair[i]
    
    ET[i] ~ dnorm(LE[i] / Lambda[i] *24*3.6, invvar)
	}

  Sigma <- 5.670367 * 10^-8
  albedo ~ dunif(0.0, 0.35)
  eSky ~ dunif(0.65, .95)
  eSur ~ dunif(0.90, .99)
  Cgrnd ~ dunif(0.1, .3)
  Cconv ~ dnorm(0, .0001)T(0,10^100)
  Aconv ~ dunif(0.5, .8)

	invvar   ~ dgamma(0.01, 0.01)
  sigma     <- 1/sqrt(invvar)
	
	for(i in 1:n){
	  ETpred[i] ~ dnorm(LE[i] / Lambda[i] *24*3.6, invvar)
	}
}